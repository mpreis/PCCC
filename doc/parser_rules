programm	=	{include} {declaration | structDec} {procedure} .

procedure	=	procHead "{" {declaration} statementSeq "}" .
procHead	=	(typeSpec ["*"]) identifier "(" [procParList] ")" .
procParList	=	typeSpec (identifier | pointer) { "," typeSpec (identifier | pointer) } .

statementSeq=	{ statement ";" } .
statement	=	init | procCall | expression | ret | while | ifCmd .

ifCmd		=	"if" "(" boolExp ")" block [elseCmd] .	
elseCmd		=	"else" (ifCmd | block) .
whileLoop	=	"while" "(" boolExp ")" block .
ret			=	"return" expression .

block		= 	"{" statementSeq "}" .

expression	=	arithExp { boolOp arithExp } .
arithExp 	= 	[ "-" ] term { ( "+" | "-" ) term } .
term 		= 	factor { ( "*" | "/" ) factor } .
factor 		= 	identifier | number | procCall | reference | "(" expression ")" .
reference	=	identifier "." identifier .
pointer		=	"*" identifier .

declaration	=	typeSpec (identifier | pointer) [ ("[" (number | identifier) "]") | ("(" [procParList] ")") ] { "," (identifier | pointer) } ";" .
structDec	=	"typedef" "struct" "{" declaration {declaration} "}" identifier ";" .

init		=	identifier ["[" (number | identifier) "]"] "=" expression .
procCall	=	identifier "(" [paramList]")" .
paramList	=	identifier {"," identifier} .

include		=	"include" fileName .
typeSpec	=	["typedef"] ("int" | "char" | "void") .

boolOp		=	"==" | "!=" | "<=" | ">=" | "<" | ">" | "&&" | "||" .

fileName	=	identifier "." identifier .
identifier	=	letter {letter | digit} .
number		=	["-"] digit {digit} .
digit		=	"0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .  
letter		=	"a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | 
				"n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" |
				"A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" |
				"N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" .
